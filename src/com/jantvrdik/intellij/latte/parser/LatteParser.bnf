{
	parserClass="com.jantvrdik.intellij.latte.parser.LatteParser"
	parserUtilClass="com.jantvrdik.intellij.latte.parser.LatteParserUtil"

	extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

	psiClassPrefix="Latte"
	psiImplClassSuffix="Impl"
	psiPackage="com.jantvrdik.intellij.latte.psi"
	psiImplPackage="com.jantvrdik.intellij.latte.psi.impl"
	psiImplUtilClass="com.jantvrdik.intellij.latte.psi.impl.LattePsiImplUtil"

	elementTypeHolderClass="com.jantvrdik.intellij.latte.psi.LatteTypes"
	elementTypeClass="com.jantvrdik.intellij.latte.psi.LatteElementType"
	tokenTypeClass="com.jantvrdik.intellij.latte.psi.LatteTokenType"
}

root             ::= (structureToken)* autoClosedBlock?

// macro
private
macro            ::= macroComment | macroClassic

macroComment     ::= T_MACRO_COMMENT
macroClassic     ::= <<checkPairMacro "pair">> (pairMacro | emptyMacro) | <<checkPairMacro "unpair">> unpairedMacro {
                     	methods = [
                     		openTag="macroTag[0]"
                     		closeTag="macroTag[1]"
                     	]
                     }

private
pairMacro        ::= macroOpenTag structureToken* macroCloseTag

private
emptyMacro       ::= emptyMacroTag;

private
unpairedMacro    ::= macroOpenTag

fake
macroTag         ::= classicMacroContent {
                     	methods = [
                     		getMacroName
                     		macroContent="macroContent"
                     	]
                     }

macroOpenTag     ::= T_MACRO_OPEN_TAG_OPEN classicMacroContent T_MACRO_TAG_CLOSE {
                        mixin="com.jantvrdik.intellij.latte.psi.impl.elements.LatteMacroTagElementImpl"
                        implements="com.jantvrdik.intellij.latte.psi.elements.LatteMacroTagElement"
                        methods=[getName setName getNameIdentifier getMacroName]
                        extends = macroTag
                     }

macroCloseTag    ::= T_MACRO_CLOSE_TAG_OPEN classicMacroContent T_MACRO_TAG_CLOSE {
                        mixin="com.jantvrdik.intellij.latte.psi.impl.elements.LatteMacroTagElementImpl"
                        implements="com.jantvrdik.intellij.latte.psi.elements.LatteMacroTagElement"
                        methods=[getName setName getNameIdentifier getMacroName]
                     	extends = macroTag
                     }

emptyMacroTag    ::= T_MACRO_OPEN_TAG_OPEN classicMacroContent T_MACRO_TAG_CLOSE_EMPTY {
                     	extends = macroTag
                     }


private
classicMacroContent     ::= macroName macroContent?

macroContent ::= macroArgs? (T_MACRO_MODIFIERS macroArgs)* {
                        methods=[getFirstPhpContent]
                    }

private
macroName        ::= T_MACRO_NAME
                   | T_MACRO_NOESCAPE? T_MACRO_SHORTNAME?

private
macroArgs        ::= phpContent

// autoClosedBlock
autoClosedBlock  ::= &("{block" macroArgs? T_MACRO_MODIFIERS? "}") macroOpenTag structureToken*



// netteAttr
netteAttr        ::= T_HTML_TAG_NATTR_NAME T_WHITESPACE* (T_HTML_TAG_ATTR_EQUAL_SIGN netteAttrValue)? {
                        methods = [
                            attrName="T_HTML_TAG_NATTR_NAME"
                            attrValue="netteAttrValue"
                        ]
                     }

netteAttrValue   ::= T_HTML_TAG_ATTR_SQ macroContent? T_HTML_TAG_ATTR_SQ
                   | T_HTML_TAG_ATTR_DQ macroContent? T_HTML_TAG_ATTR_DQ
                   | macroContent?

outerHtml ::= T_TEXT;

// PHP tokens
phpContent       ::= (phpForeach | phpFor | phpExpression)+

phpVariable        ::= T_MACRO_ARGS_VAR {
                        mixin="com.jantvrdik.intellij.latte.psi.impl.elements.LattePhpVariableElementImpl"
                        implements="com.jantvrdik.intellij.latte.psi.elements.LattePhpVariableElement"
                        methods=[getName setName getNameIdentifier getVariableName isDefinition getPhpType isVarTypeDefinition]
                    }

phpStaticVariable  ::= T_MACRO_ARGS_VAR {
                        mixin="com.jantvrdik.intellij.latte.psi.impl.elements.LattePhpStaticVariableElementImpl"
                        implements="com.jantvrdik.intellij.latte.psi.elements.LattePhpStaticVariableElement"
                        methods=[getName setName getNameIdentifier getVariableName getPhpType getPropertyType]
                    }

phpMethod        ::= T_PHP_METHOD {
                        mixin="com.jantvrdik.intellij.latte.psi.impl.elements.LattePhpMethodElementImpl"
                        implements="com.jantvrdik.intellij.latte.psi.elements.LattePhpMethodElement"
                        methods=[getName setName getNameIdentifier getMethodName getPhpType getReturnType isStatic isFunction]
                    }

phpConstant        ::= T_PHP_METHOD {
                        mixin="com.jantvrdik.intellij.latte.psi.impl.elements.LattePhpConstantElementImpl"
                        implements="com.jantvrdik.intellij.latte.psi.elements.LattePhpConstantElement"
                        methods=[getName setName getNameIdentifier getConstantName getPhpType getConstantType]
                    }

phpProperty        ::= T_PHP_METHOD {
                        mixin="com.jantvrdik.intellij.latte.psi.impl.elements.LattePhpPropertyElementImpl"
                        implements="com.jantvrdik.intellij.latte.psi.elements.LattePhpPropertyElement"
                        methods=[getName setName getNameIdentifier getPropertyName getPhpType getPropertyType isStatic]
                    }

phpClass        ::= T_PHP_VAR_TYPE {
                        mixin="com.jantvrdik.intellij.latte.psi.impl.elements.LattePhpClassElementImpl"
                        implements="com.jantvrdik.intellij.latte.psi.elements.LattePhpClassElement"
                        methods=[getName setName getNameIdentifier getClassName getPhpType]
                    }

phpForeach       ::= T_WHITESPACE? phpExpression T_WHITESPACE? T_PHP_AS T_WHITESPACE? (phpVariable | phpArrayOfVariables) (T_WHITESPACE? T_PHP_DOUBLE_ARROW T_WHITESPACE? (phpVariable | phpArrayOfVariables))?

phpFor          ::= T_WHITESPACE? phpExpression* T_WHITESPACE? ";" T_WHITESPACE? phpExpression* T_WHITESPACE? ";" T_WHITESPACE? phpExpression* T_WHITESPACE?

phpArrayOfVariables        ::= T_WHITESPACE? T_PHP_LEFT_BRACKET T_WHITESPACE? phpVariable ( T_WHITESPACE? "," T_WHITESPACE? phpVariable )* ","? T_WHITESPACE? T_PHP_RIGHT_BRACKET T_WHITESPACE?

private
phpMethodDefinition       ::= T_WHITESPACE? (T_PHP_DOUBLE_COLON | T_PHP_OBJECT_OPERATOR) T_WHITESPACE? phpMethod T_WHITESPACE? phpArgumentList T_WHITESPACE?

private
phpArgumentList       ::= (T_PHP_LEFT_NORMAL_BRACE T_WHITESPACE? T_PHP_RIGHT_NORMAL_BRACE) | (T_PHP_LEFT_NORMAL_BRACE phpMethodArgs T_PHP_RIGHT_NORMAL_BRACE)

private
phpInBrackets       ::= T_PHP_LEFT_NORMAL_BRACE T_WHITESPACE? phpArgument+ T_WHITESPACE? T_PHP_RIGHT_NORMAL_BRACE

phpMethodArgs       ::= T_WHITESPACE? phpArgument+ ( T_WHITESPACE? "," T_WHITESPACE? phpArgument+ )* T_WHITESPACE?

private
phpArgument    ::=  T_MACRO_ARGS_STRING | T_MACRO_ARGS_NUMBER | phpClass | phpDefinition
                    | phpStatic | phpFunctionCall | phpInBrackets | phpVariable | T_PHP_OBJECT_OPERATOR
                    | T_PHP_OPERATOR | T_PHP_DOUBLE_COLON | T_PHP_DOUBLE_ARROW | T_PHP_METHOD | T_PHP_TYPE | T_PHP_KEYWORD
                    | T_PHP_CLASS | T_PHP_AS | T_PHP_CAST | T_PHP_EXPRESSION | T_PHP_LEFT_BRACKET | T_PHP_RIGHT_BRACKET
                    | T_PHP_LEFT_CURLY_BRACE | T_PHP_RIGHT_CURLY_BRACE

private
phpFunctionCall    ::= (phpMethod | phpVariable) T_WHITESPACE? phpArgumentList T_WHITESPACE?

private
phpExpression    ::= T_MACRO_ARGS | phpArgument | T_PHP_LEFT_NORMAL_BRACE | T_PHP_RIGHT_NORMAL_BRACE

private
phpDefinition    ::= phpMethodDefinition | phpConstantDefinition | phpPropertyDefinition

private
phpStatic ::= T_WHITESPACE? T_PHP_DOUBLE_COLON T_WHITESPACE? phpStaticVariable

private
phpConstantDefinition ::= T_WHITESPACE? T_PHP_DOUBLE_COLON T_WHITESPACE? phpConstant

private
phpPropertyDefinition ::= T_WHITESPACE? T_PHP_OBJECT_OPERATOR T_WHITESPACE? phpProperty

// tokens sets
private
structureToken   ::= outerHtml | macro | netteAttr


private
topLevelTokens   ::= T_MACRO_CLASSIC T_MACRO_CONTENT T_PHP_CONTENT topLevelTokens
